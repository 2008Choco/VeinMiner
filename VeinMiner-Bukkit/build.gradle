import org.apache.tools.ant.filters.*

plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

repositories {
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://repo.codemc.org/repository/maven-public/' }

    maven { url 'http://nexus.hc.to/content/repositories/pub_releases/' }
    maven { url 'https://maven.enginehub.org/repo/' }

    maven { url 'https://jitpack.io' }
    maven { url 'https://repo.md-5.net/content/repositories/snapshots/' }
    maven { url 'https://repo.janmm14.de/repository/public/' }
}

dependencies {
    implementation 'org.bstats:bstats-bukkit:2.2.1'

    compileOnly 'org.bukkit:bukkit:1.13.2-R0.1-SNAPSHOT'
    compileOnly 'org.jetbrains:annotations:18.0.0'

    compileOnly 'net.milkbowl.vault:VaultAPI:1.7'

    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.3'
    // GitHub master, version 2.1.175-SNAPSHOT
    // https://github.com/mcMMO-Dev/mcMMO/commit/085c8dbf68b8503f84b92c8adbde94631707effe
    compileOnly 'com.github.mcMMO-Dev:mcMMO:085c8db'

    // Anti-cheats
    compileOnly 'fr.neatmonster:nocheatplus:3.16.1-SNAPSHOT'
    compileOnly 'de.janmm14:aac-api:5.0.0'
    compileOnly 'com.github.jiangdashao:matrix-api-repo:317d4635fd'
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

processResources {
    filter ReplaceTokens, tokens: [
        "project.version": version
    ]
}

shadowJar {
    relocate 'org.bstats', 'wtf.choco.veinminer.metrics'
    archiveFileName = "VeinMiner-Bukkit-${version}.jar"
}

tasks.build.dependsOn tasks.shadowJar
