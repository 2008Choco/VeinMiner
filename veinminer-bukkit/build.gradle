import org.apache.tools.ant.filters.*

plugins {
    alias libs.plugins.shadow
}

javadoc {
    exclude "wtf/choco/veinminer/listener/**"
}

repositories {
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' } // Spigot

    maven { url 'https://nexus.hc.to/content/repositories/pub_releases/' } // Vault API
    maven { url 'https://maven.enginehub.org/repo/' } // WorldGuard

    maven { url 'https://jitpack.io' } // mcMMO, Matrix AntiCheat
    maven { url 'https://repo.janmm14.de/repository/public/' } // Advanced AntiCheat
    maven { url 'https://repo.md-5.net/content/repositories/snapshots/' } // NoCheatPlus
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' } // PlaceholderAPI
}

dependencies {
    compileOnly libs.spigot.api

    implementation project(':veinminer-common')
    implementation project(':veinminer-server')

    implementation libs.bstats.bukkit
    implementation libs.choco.networking.bukkit

    compileOnly libs.placeholder.api
    compileOnly libs.vault.api
    compileOnly libs.worldguard
    compileOnly libs.mcmmo

    // Anti-cheats
    compileOnly libs.anticheat.aac
    compileOnly libs.anticheat.grim
    compileOnly libs.anticheat.matrix
    compileOnly libs.anticheat.nocheatplus
}

processResources {
    filter ReplaceTokens, tokens: [
        "project.version": version
    ]
}

shadowJar {
    relocate 'org.bstats', 'wtf.choco.veinminer.metrics'
    archiveFileName = "VeinMiner-Bukkit-${version}.jar"

    dependencies {
        include(project(':veinminer-common'))
        include(project(':veinminer-server'))
        include(dependency(libs.bstats.base.get()))
        include(dependency(libs.bstats.bukkit.get()))
        include(dependency(libs.choco.networking.common.get()))
        include(dependency(libs.choco.networking.bukkit.get()))
    }
}

tasks.build.dependsOn tasks.shadowJar

/*
 * Shadow plugin is stupid and automatically configures the publish plugin to publish a shadowed .jar
 * We don't want that... so we're gonna skip it
 */
components.java.withVariantsFromConfiguration(configurations.shadowRuntimeElements) {
    skip()
}

publishing {
    publications {
        bukkit(MavenPublication) {
            from components.java
            artifactId = project.name
        }
    }
}
