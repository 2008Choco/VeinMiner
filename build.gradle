plugins {
    id 'eclipse'
    id 'maven-publish'
    alias libs.plugins.aggregate.javadoc
}

allprojects {
    group 'wtf.choco'
    version '2.2.0'

    tasks.withType(JavaCompile) {
        options.encoding 'UTF-8'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'

    repositories {
        mavenCentral()
        maven { url = 'http://repo.choco.wtf/releases'; allowInsecureProtocol = true }
    }

    dependencies {
        compileOnly rootProject.libs.jetbrains.annotations
        implementation rootProject.libs.choco.networking.common
    }

    java {
        withJavadocJar()
        withSourcesJar()

        toolchain {
            languageVersion.set(JavaLanguageVersion.of(17))
        }
    }

    def javadocConfig = {
        options.links(
            'https://docs.oracle.com/en/java/javase/17/docs/api/',
            'https://hub.spigotmc.org/javadocs/spigot/'
        )

        options.tags(
            "apiNote:a:API Note:",
            "implSpec:a:Implementation Specification:",
            "implNote:a:Implementation Note:"
        )
    }

    javadoc {
        configure javadocConfig
    }

    aggregateJavadoc {
        configure javadocConfig
    }

    tasks.withType(Checkstyle) {
        configFile = file("${rootDir}/checkstyle.xml")
    }
}

publishing {
    repositories {
        maven {
            allowInsecureProtocol = true // I'll fix this once I get an SSL cert for my repository :)

            def repository = version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'
            url = "http://repo.choco.wtf/$repository"

            credentials {
                username property('mavenUsername')
                password property('mavenAccessToken')
            }
        }
    }
}
