plugins {
    id 'base'
    id 'eclipse'
    id 'maven-publish'
    id "io.freefair.aggregate-javadoc" version "6.5.0.3"
}

allprojects {
    group 'wtf.choco' // Group for each version
    version '2.0.0' // Global version applied to all projects

    tasks.withType(JavaCompile) {
        options.encoding 'UTF-8'
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'checkstyle'

    repositories { // Repos applied to all sub projects
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    dependencies {
        compileOnly 'org.jetbrains:annotations:23.0.0'
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    def javadocConfig = {
        options {
            links(
                'https://docs.oracle.com/en/java/javase/17/docs/api/',
                'https://hub.spigotmc.org/javadocs/spigot/'
            )

            tags(
                "apiNote:a:API Note:",
                "implSpec:a:Implementation Specification:",
                "implNote:a:Implementation Note:"
            )
        }
    }

    javadoc { configure javadocConfig }
    aggregateJavadoc { configure javadocConfig }

    checkstyleMain {
        configFile = file("${rootDir}/checkstyle.xml")
    }

    checkstyleTest {
        configFile = file("${rootDir}/checkstyle.xml")
    }
}

publishing {
    repositories {
        maven {
            allowInsecureProtocol = true // I'll fix this once I get an SSL cert for my repository :)

            def repository = version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'
            url = "http://repo.choco.wtf/$repository"

            credentials {
                username property('mavenUsername')
                password property('mavenAccessToken')
            }
        }
    }
}
