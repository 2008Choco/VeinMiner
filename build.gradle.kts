plugins {
    id("eclipse")
    id("java")
    id("maven-publish")
    // alias(libs.plugins.aggregate.javadoc) // Stupid idiot dumb plugin checks if "java" plugin is used on root, but can't use compileOnly() or implementation() UNLESS the "java" plugin is applied. So I guess fuck me then
}

/* Meant for Aggregate Javadocs but lol the plugin is stupid
dependencies {
    rootProject.subprojects.forEach { subproject ->
        subproject.plugins.withId("java") {
            javadoc(subproject)
        }
    }
}
*/

subprojects {
    apply(plugin = "checkstyle")
    apply(plugin = "java")
    apply(plugin = "maven-publish")

    group = "wtf.choco"
    version = "2.2.6"

    repositories {
        mavenCentral()
        maven { url = uri("https://repo.choco.wtf/releases"); }
        maven { url = uri("https://repo.choco.wtf/snapshots"); } // Themis
        maven { url = uri("https://repo.grim.ac/snapshots"); } // Grim Anticheat
    }

    dependencies {
        compileOnly(rootProject.libs.jetbrains.annotations)
        implementation(rootProject.libs.choco.networking.common)
    }

    java {
        withJavadocJar()
        withSourcesJar()

        toolchain {
            languageVersion.set(JavaLanguageVersion.of(17))
        }
    }

    tasks {
        withType<JavaCompile> {
            options.release = 17
            options.encoding = Charsets.UTF_8.name()
        }

        withType<Javadoc> {
            val options = (options as org.gradle.external.javadoc.StandardJavadocDocletOptions)
            options.encoding = Charsets.UTF_8.name()
            
            options.links(
                "https://docs.oracle.com/en/java/javase/17/docs/api/",
                "https://hub.spigotmc.org/javadocs/spigot"
            )

            options.tags(
                "apiNote:a:API Note:",
                "implSpec:a:Implementation Specification",
                "implNote:a:Implementation Note"
            )
        }

        withType<Checkstyle> {
            configFile = file("${rootDir}/checkstyle.xml")
        }

        withType<Test> {
            useJUnitPlatform()
        }
    }

    publishing {
        repositories {
            maven {
                val repository = if (project.version.toString().endsWith("SNAPSHOT")) "snapshots" else "releases"
                url = uri("https://repo.choco.wtf/$repository")

                credentials {
                    username = project.properties["mavenUsername"].toString()
                    password = project.properties["mavenAccessToken"].toString()
                }
            }
        }

        publications {
            create<MavenPublication>("maven") {
                from(components["java"])
            }
        }
    }
}
